<?xml version="1.0"?>
<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
    "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
<html xmlns="http://www.w3.org/1999/xhtml" xml:lang="en" lang="en">
<head><meta charset="utf-8">
<meta http-equiv="cache-control" content="no-cache" />
<meta http-equiv="Pragma" content="no-cache" />
<meta http-equiv="Expires" content="-1" />
<!--
   Note to customizers: the body of the webrev is IDed as SUNWwebrev
   to allow easy overriding by users of webrev via the userContent.css
   mechanism available in some browsers.

   For example, to have all "removed" information be red instead of
   brown, set a rule in your userContent.css file like:

       body#SUNWwebrev span.removed { color: red ! important; }
-->
<style type="text/css" media="screen">
body {
    background-color: #eeeeee;
}
hr {
    border: none 0;
    border-top: 1px solid #aaa;
    height: 1px;
}
div.summary {
    font-size: .8em;
    border-bottom: 1px solid #aaa;
    padding-left: 1em;
    padding-right: 1em;
}
div.summary h2 {
    margin-bottom: 0.3em;
}
div.summary table th {
    text-align: right;
    vertical-align: top;
    white-space: nowrap;
}
span.lineschanged {
    font-size: 0.7em;
}
span.oldmarker {
    color: red;
    font-size: large;
    font-weight: bold;
}
span.newmarker {
    color: green;
    font-size: large;
    font-weight: bold;
}
span.removed {
    color: brown;
}
span.changed {
    color: blue;
}
span.new {
    color: blue;
    font-weight: bold;
}
a.print { font-size: x-small; }

</style>

<style type="text/css" media="print">
pre { font-size: 0.8em; font-family: courier, monospace; }
span.removed { color: #444; font-style: italic }
span.changed { font-weight: bold; }
span.new { font-weight: bold; }
span.newmarker { font-size: 1.2em; font-weight: bold; }
span.oldmarker { font-size: 1.2em; font-weight: bold; }
a.print {display: none}
hr { border: none 0; border-top: 1px solid #aaa; height: 1px; }
</style>

<title>New common/autoconf/platform.m4</title>
<body id="SUNWwebrev">
<pre>
   1 #
   2 # Copyright (c) 2011, 2012, Oracle and/or its affiliates. All rights reserved.
   3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
   4 #
   5 # This code is free software; you can redistribute it and/or modify it
   6 # under the terms of the GNU General Public License version 2 only, as
   7 # published by the Free Software Foundation.  Oracle designates this
   8 # particular file as subject to the "Classpath" exception as provided
   9 # by Oracle in the LICENSE file that accompanied this code.
  10 #
  11 # This code is distributed in the hope that it will be useful, but WITHOUT
  12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
  13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
  14 # version 2 for more details (a copy is included in the LICENSE file that
  15 # accompanied this code).
  16 #
  17 # You should have received a copy of the GNU General Public License version
  18 # 2 along with this work; if not, write to the Free Software Foundation,
  19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
  20 #
  21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
  22 # or visit www.oracle.com if you need additional information or have any
  23 # questions.
  24 #
  25 
  26 # Support macro for PLATFORM_EXTRACT_TARGET_AND_BUILD.
  27 # Converts autoconf style CPU name to OpenJDK style, into
  28 # VAR_CPU, VAR_CPU_ARCH, VAR_CPU_BITS and VAR_CPU_ENDIAN.
  29 AC_DEFUN([PLATFORM_EXTRACT_VARS_FROM_CPU],
  30 [
  31   # First argument is the cpu name from the trip/quad
  32   case "$1" in
  33     x86_64)
  34       VAR_CPU=x86_64
  35       VAR_CPU_ARCH=x86
  36       VAR_CPU_BITS=64
  37       VAR_CPU_ENDIAN=little
  38       ;;
  39     i?86)
  40       VAR_CPU=x86
  41       VAR_CPU_ARCH=x86
  42       VAR_CPU_BITS=32
  43       VAR_CPU_ENDIAN=little
  44       ;;
  45     arm*)
  46       VAR_CPU=arm
  47       VAR_CPU_ARCH=arm
  48       VAR_CPU_BITS=32
  49       VAR_CPU_ENDIAN=little
  50       ;;
  51     powerpc)
  52       VAR_CPU=ppc
  53       VAR_CPU_ARCH=ppc
  54       VAR_CPU_BITS=32
  55       VAR_CPU_ENDIAN=big
  56       ;;
  57     powerpc64)
  58       VAR_CPU=ppc64
  59       VAR_CPU_ARCH=ppc
  60       VAR_CPU_BITS=64
  61       VAR_CPU_ENDIAN=big
  62       ;;
  63     powerpc64le)
  64       VAR_CPU=ppc64
  65       VAR_CPU_ARCH=ppc
  66       VAR_CPU_BITS=64
  67       VAR_CPU_ENDIAN=little
  68       ;;
  69     s390)
  70       VAR_CPU=s390
  71       VAR_CPU_ARCH=s390
  72       VAR_CPU_BITS=32
  73       VAR_CPU_ENDIAN=big
  74       ;;
  75     s390x)
  76       VAR_CPU=s390x
  77       VAR_CPU_ARCH=s390
  78       VAR_CPU_BITS=64
  79       VAR_CPU_ENDIAN=big
  80       ;;
  81     sparc)
  82       VAR_CPU=sparc
  83       VAR_CPU_ARCH=sparc
  84       VAR_CPU_BITS=32
  85       VAR_CPU_ENDIAN=big
  86       ;;
  87     sparcv9|sparc64)
  88       VAR_CPU=sparcv9
  89       VAR_CPU_ARCH=sparc
  90       VAR_CPU_BITS=64
  91       VAR_CPU_ENDIAN=big
  92       ;;
  93     *)
  94       AC_MSG_ERROR([unsupported cpu $1])
  95       ;;
  96   esac
  97 ])
  98 
  99 # Support macro for PLATFORM_EXTRACT_TARGET_AND_BUILD.
 100 # Converts autoconf style OS name to OpenJDK style, into
 101 # VAR_OS, VAR_OS_TYPE and VAR_OS_ENV.
 102 AC_DEFUN([PLATFORM_EXTRACT_VARS_FROM_OS],
 103 [
 104   case "$1" in
 105     *linux*)
 106       VAR_OS=linux
 107       VAR_OS_TYPE=unix
 108       ;;
 109     *solaris*)
 110       VAR_OS=solaris
 111       VAR_OS_TYPE=unix
 112       ;;
 113     *darwin*)
 114       VAR_OS=macosx
 115       VAR_OS_TYPE=unix
 116       ;;
 117     *bsd*)
 118       VAR_OS=bsd
 119       VAR_OS_TYPE=unix
 120       ;;
 121     *cygwin*)
 122       VAR_OS=windows
 123       VAR_OS_ENV=windows.cygwin
 124       ;;
 125     *mingw*)
 126       VAR_OS=windows
 127       VAR_OS_ENV=windows.msys
 128       ;;
 129     *aix*)
 130       VAR_OS=aix
 131       VAR_OS_TYPE=unix
 132       ;;
 133     *)
 134       AC_MSG_ERROR([unsupported operating system $1])
 135       ;;
 136   esac
 137 ])
 138 
 139 # Expects $host_os $host_cpu $build_os and $build_cpu
 140 # and $with_target_bits to have been setup!
 141 #
 142 # Translate the standard triplet(quadruplet) definition
 143 # of the target/build system into OPENJDK_TARGET_OS, OPENJDK_TARGET_CPU,
 144 # OPENJDK_BUILD_OS, etc.
 145 AC_DEFUN([PLATFORM_EXTRACT_TARGET_AND_BUILD],
 146 [
 147   # Copy the autoconf trip/quadruplet verbatim to OPENJDK_TARGET_AUTOCONF_NAME
 148   # (from the autoconf "host") and OPENJDK_BUILD_AUTOCONF_NAME
 149   # Note that we might later on rewrite e.g. OPENJDK_TARGET_CPU due to reduced build,
 150   # but this will not change the value of OPENJDK_TARGET_AUTOCONF_NAME.
 151   OPENJDK_TARGET_AUTOCONF_NAME="$host"
 152   OPENJDK_BUILD_AUTOCONF_NAME="$build"
 153   AC_SUBST(OPENJDK_TARGET_AUTOCONF_NAME)
 154   AC_SUBST(OPENJDK_BUILD_AUTOCONF_NAME)
 155 
 156   # Convert the autoconf OS/CPU value to our own data, into the VAR_OS/CPU variables.
 157   PLATFORM_EXTRACT_VARS_FROM_OS($build_os)
 158   PLATFORM_EXTRACT_VARS_FROM_CPU($build_cpu)
 159   # ..and setup our own variables. (Do this explicitely to facilitate searching)
 160   OPENJDK_BUILD_OS="$VAR_OS"
 161   if test "x$VAR_OS_TYPE" != x; then
 162     OPENJDK_BUILD_OS_TYPE="$VAR_OS_TYPE"
 163   else
 164     OPENJDK_BUILD_OS_TYPE="$VAR_OS"
 165   fi
 166   if test "x$VAR_OS_ENV" != x; then
 167     OPENJDK_BUILD_OS_ENV="$VAR_OS_ENV"
 168   else
 169     OPENJDK_BUILD_OS_ENV="$VAR_OS"
 170   fi
 171   OPENJDK_BUILD_CPU="$VAR_CPU"
 172   OPENJDK_BUILD_CPU_ARCH="$VAR_CPU_ARCH"
 173   OPENJDK_BUILD_CPU_BITS="$VAR_CPU_BITS"
 174   OPENJDK_BUILD_CPU_ENDIAN="$VAR_CPU_ENDIAN"
 175   AC_SUBST(OPENJDK_BUILD_OS)
 176   AC_SUBST(OPENJDK_BUILD_OS_TYPE)
 177   AC_SUBST(OPENJDK_BUILD_OS_ENV)
 178   AC_SUBST(OPENJDK_BUILD_CPU)
 179   AC_SUBST(OPENJDK_BUILD_CPU_ARCH)
 180   AC_SUBST(OPENJDK_BUILD_CPU_BITS)
 181   AC_SUBST(OPENJDK_BUILD_CPU_ENDIAN)
 182 
 183   AC_MSG_CHECKING([openjdk-build os-cpu])
 184   AC_MSG_RESULT([$OPENJDK_BUILD_OS-$OPENJDK_BUILD_CPU])
 185 
 186   # Convert the autoconf OS/CPU value to our own data, into the VAR_OS/CPU variables.
 187   PLATFORM_EXTRACT_VARS_FROM_OS($host_os)
 188   PLATFORM_EXTRACT_VARS_FROM_CPU($host_cpu)
 189   # ... and setup our own variables. (Do this explicitely to facilitate searching)
 190   OPENJDK_TARGET_OS="$VAR_OS"
 191   if test "x$VAR_OS_TYPE" != x; then
 192     OPENJDK_TARGET_OS_TYPE="$VAR_OS_TYPE"
 193   else
 194     OPENJDK_TARGET_OS_TYPE="$VAR_OS"
 195   fi
 196   if test "x$VAR_OS_ENV" != x; then
 197     OPENJDK_TARGET_OS_ENV="$VAR_OS_ENV"
 198   else
 199     OPENJDK_TARGET_OS_ENV="$VAR_OS"
 200   fi
 201   OPENJDK_TARGET_CPU="$VAR_CPU"
 202   OPENJDK_TARGET_CPU_ARCH="$VAR_CPU_ARCH"
 203   OPENJDK_TARGET_CPU_BITS="$VAR_CPU_BITS"
 204   OPENJDK_TARGET_CPU_ENDIAN="$VAR_CPU_ENDIAN"
 205   AC_SUBST(OPENJDK_TARGET_OS)
 206   AC_SUBST(OPENJDK_TARGET_OS_TYPE)
 207   AC_SUBST(OPENJDK_TARGET_OS_ENV)
 208   AC_SUBST(OPENJDK_TARGET_CPU)
 209   AC_SUBST(OPENJDK_TARGET_CPU_ARCH)
 210   AC_SUBST(OPENJDK_TARGET_CPU_BITS)
 211   AC_SUBST(OPENJDK_TARGET_CPU_ENDIAN)
 212 
 213   AC_MSG_CHECKING([openjdk-target os-cpu])
 214   AC_MSG_RESULT([$OPENJDK_TARGET_OS-$OPENJDK_TARGET_CPU])
 215 ])
 216 
 217 # Check if a reduced build (32-bit on 64-bit platforms) is requested, and modify behaviour
 218 # accordingly. Must be done after setting up build and target system, but before
 219 # doing anything else with these values.
 220 AC_DEFUN([PLATFORM_SETUP_TARGET_CPU_BITS],
 221 [
 222   AC_ARG_WITH(target-bits, [AS_HELP_STRING([--with-target-bits],
 223        [build 32-bit or 64-bit binaries (for platforms that support it), e.g. --with-target-bits=32 @&lt;:@guessed@:&gt;@])])
 224 
 225   # We have three types of compiles:
 226   # native  == normal compilation, target system == build system
 227   # cross   == traditional cross compilation, target system != build system; special toolchain needed
 228   # reduced == using native compilers, but with special flags (e.g. -m32) to produce 32-bit builds on 64-bit machines
 229   #
 230   if test "x$OPENJDK_BUILD_AUTOCONF_NAME" != "x$OPENJDK_TARGET_AUTOCONF_NAME"; then
 231     # We're doing a proper cross-compilation
 232     COMPILE_TYPE="cross"
 233   else
 234     COMPILE_TYPE="native"
 235   fi
 236 
 237   if test "x$with_target_bits" != x; then
 238     if test "x$COMPILE_TYPE" = "xcross"; then
 239       AC_MSG_ERROR([It is not possible to combine --with-target-bits=X and proper cross-compilation. Choose either.])
 240     fi
 241 
 242     if test "x$with_target_bits" = x32 &amp;&amp; test "x$OPENJDK_TARGET_CPU_BITS" = x64; then
 243       # A reduced build is requested
 244       COMPILE_TYPE="reduced"
 245       OPENJDK_TARGET_CPU_BITS=32
 246       if test "x$OPENJDK_TARGET_CPU_ARCH" = "xx86"; then
 247         OPENJDK_TARGET_CPU=x86
 248       elif test "x$OPENJDK_TARGET_CPU_ARCH" = "xsparc"; then
 249         OPENJDK_TARGET_CPU=sparc
 250       else
 251         AC_MSG_ERROR([Reduced build (--with-target-bits=32) is only supported on x86_64 and sparcv9])
 252       fi
 253     elif test "x$with_target_bits" = x64 &amp;&amp; test "x$OPENJDK_TARGET_CPU_BITS" = x32; then
 254       AC_MSG_ERROR([It is not possible to use --with-target-bits=64 on a 32 bit system. Use proper cross-compilation instead.])
 255     elif test "x$with_target_bits" = "x$OPENJDK_TARGET_CPU_BITS"; then
 256       AC_MSG_NOTICE([--with-target-bits are set to build platform address size; argument has no meaning])
 257     else
 258       AC_MSG_ERROR([--with-target-bits can only be 32 or 64, you specified $with_target_bits!])
 259     fi
 260   fi
 261   AC_SUBST(COMPILE_TYPE)
 262 
 263   AC_MSG_CHECKING([compilation type])
 264   AC_MSG_RESULT([$COMPILE_TYPE])
 265 ])
 266 
 267 # Setup the legacy variables, for controlling the old makefiles.
 268 #
 269 AC_DEFUN([PLATFORM_SETUP_LEGACY_VARS],
 270 [
 271   # Also store the legacy naming of the cpu.
 272   # Ie i586 and amd64 instead of x86 and x86_64
 273   OPENJDK_TARGET_CPU_LEGACY="$OPENJDK_TARGET_CPU"
 274   if test "x$OPENJDK_TARGET_CPU" = xx86; then
 275     OPENJDK_TARGET_CPU_LEGACY="i586"
 276   elif test "x$OPENJDK_TARGET_OS" != xmacosx &amp;&amp; test "x$OPENJDK_TARGET_CPU" = xx86_64; then
 277     # On all platforms except MacOSX replace x86_64 with amd64.
 278     OPENJDK_TARGET_CPU_LEGACY="amd64"
 279   fi
 280   AC_SUBST(OPENJDK_TARGET_CPU_LEGACY)
 281 
 282   # And the second legacy naming of the cpu.
 283   # Ie i386 and amd64 instead of x86 and x86_64.
 284   OPENJDK_TARGET_CPU_LEGACY_LIB="$OPENJDK_TARGET_CPU"
 285   if test "x$OPENJDK_TARGET_CPU" = xx86; then
 286     OPENJDK_TARGET_CPU_LEGACY_LIB="i386"
 287   elif test "x$OPENJDK_TARGET_CPU" = xx86_64; then
 288     OPENJDK_TARGET_CPU_LEGACY_LIB="amd64"
 289   elif test "x$OPENJDK_TARGET_CPU" = xppc64 &amp;&amp; test "x$OPENJDK_TARGET_CPU_ENDIAN" = xlittle; then
 290     OPENJDK_TARGET_CPU_LEGACY_LIB="ppc64le"
 291   fi
 292   AC_SUBST(OPENJDK_TARGET_CPU_LEGACY_LIB)
 293 
 294   # This is the name of the cpu (but using i386 and amd64 instead of
 295   # x86 and x86_64, respectively), preceeded by a /, to be used when
 296   # locating libraries. On macosx, it's empty, though.
 297   OPENJDK_TARGET_CPU_LIBDIR="/$OPENJDK_TARGET_CPU_LEGACY_LIB"
 298   if test "x$OPENJDK_TARGET_OS" = xmacosx; then
 299     OPENJDK_TARGET_CPU_LIBDIR=""
 300   fi
 301   AC_SUBST(OPENJDK_TARGET_CPU_LIBDIR)
 302 
 303   # OPENJDK_TARGET_CPU_ISADIR is normally empty. On 64-bit Solaris systems, it is set to
 304   # /amd64 or /sparcv9. This string is appended to some library paths, like this:
 305   # /usr/lib${OPENJDK_TARGET_CPU_ISADIR}/libexample.so
 306   OPENJDK_TARGET_CPU_ISADIR=""
 307   if test "x$OPENJDK_TARGET_OS" = xsolaris; then
 308     if test "x$OPENJDK_TARGET_CPU" = xx86_64; then
 309       OPENJDK_TARGET_CPU_ISADIR="/amd64"
 310     elif test "x$OPENJDK_TARGET_CPU" = xsparcv9; then
 311       OPENJDK_TARGET_CPU_ISADIR="/sparcv9"
 312     fi
 313   fi
 314   AC_SUBST(OPENJDK_TARGET_CPU_ISADIR)
 315 
 316   # Setup OPENJDK_TARGET_CPU_OSARCH, which is used to set the os.arch Java system property
 317   OPENJDK_TARGET_CPU_OSARCH="$OPENJDK_TARGET_CPU"
 318   if test "x$OPENJDK_TARGET_OS" = xlinux &amp;&amp; test "x$OPENJDK_TARGET_CPU" = xx86; then
 319     # On linux only, we replace x86 with i386.
 320     OPENJDK_TARGET_CPU_OSARCH="i386"
 321   elif test "x$OPENJDK_TARGET_OS" != xmacosx &amp;&amp; test "x$OPENJDK_TARGET_CPU" = xx86_64; then
 322     # On all platforms except macosx, we replace x86_64 with amd64.
 323     OPENJDK_TARGET_CPU_OSARCH="amd64"
 324   elif test "x$OPENJDK_TARGET_CPU" = xppc64 &amp;&amp; test "x$OPENJDK_TARGET_CPU_ENDIAN" = xlittle; then
 325     OPENJDK_TARGET_CPU_OSARCH="ppc64le"
 326   fi
 327   AC_SUBST(OPENJDK_TARGET_CPU_OSARCH)
 328 
 329   OPENJDK_TARGET_CPU_JLI="$OPENJDK_TARGET_CPU"
 330   if test "x$OPENJDK_TARGET_CPU" = xx86; then
 331     OPENJDK_TARGET_CPU_JLI="i386"
 332   elif test "x$OPENJDK_TARGET_OS" != xmacosx &amp;&amp; test "x$OPENJDK_TARGET_CPU" = xx86_64; then
 333     # On all platforms except macosx, we replace x86_64 with amd64.
 334     OPENJDK_TARGET_CPU_JLI="amd64"
 335   elif test "x$OPENJDK_TARGET_CPU" = xppc64 &amp;&amp; test "x$OPENJDK_TARGET_CPU_ENDIAN" = xlittle; then
 336     OPENJDK_TARGET_CPU_JLI="ppc64le"
 337   fi
 338   # Now setup the -D flags for building libjli.
 339   OPENJDK_TARGET_CPU_JLI_CFLAGS="-DLIBARCHNAME='\"$OPENJDK_TARGET_CPU_JLI\"'"
 340   if test "x$OPENJDK_TARGET_OS" = xsolaris; then
 341     if test "x$OPENJDK_TARGET_CPU_ARCH" = xsparc; then
 342       OPENJDK_TARGET_CPU_JLI_CFLAGS="$OPENJDK_TARGET_CPU_JLI_CFLAGS -DLIBARCH32NAME='\"sparc\"' -DLIBARCH64NAME='\"sparcv9\"'"
 343     elif test "x$OPENJDK_TARGET_CPU_ARCH" = xx86; then
 344       OPENJDK_TARGET_CPU_JLI_CFLAGS="$OPENJDK_TARGET_CPU_JLI_CFLAGS -DLIBARCH32NAME='\"i386\"' -DLIBARCH64NAME='\"amd64\"'"
 345     fi
 346   fi
 347   AC_SUBST(OPENJDK_TARGET_CPU_JLI_CFLAGS)
 348 
 349   if test "x$OPENJDK_TARGET_OS" = xmacosx; then
 350       OPENJDK_TARGET_OS_EXPORT_DIR=macosx
 351   else
 352       OPENJDK_TARGET_OS_EXPORT_DIR=${OPENJDK_TARGET_OS_TYPE}
 353   fi
 354   AC_SUBST(OPENJDK_TARGET_OS_EXPORT_DIR)
 355 
 356   if test "x$OPENJDK_TARGET_CPU_BITS" = x64; then
 357     A_LP64="LP64:="
 358     # -D_LP64=1 is only set on linux and mac. Setting on windows causes diff in
 359     # unpack200.exe
 360     if test "x$OPENJDK_TARGET_OS" = xlinux || test "x$OPENJDK_TARGET_OS" = xmacosx; then
 361       ADD_LP64="-D_LP64=1"
 362     fi
 363   fi
 364   AC_SUBST(LP64,$A_LP64)
 365 
 366   if test "x$COMPILE_TYPE" = "xcross"; then
 367     # FIXME: ... or should this include reduced builds..?
 368     DEFINE_CROSS_COMPILE_ARCH="CROSS_COMPILE_ARCH:=$OPENJDK_TARGET_CPU_LEGACY"
 369   else
 370     DEFINE_CROSS_COMPILE_ARCH=""
 371   fi
 372   AC_SUBST(DEFINE_CROSS_COMPILE_ARCH)
 373 
 374   # ZERO_ARCHDEF is used to enable architecture-specific code
 375   case "${OPENJDK_TARGET_CPU}" in
 376     ppc)     ZERO_ARCHDEF=PPC32 ;;
 377     ppc64)   ZERO_ARCHDEF=PPC64 ;;
 378     s390*)   ZERO_ARCHDEF=S390  ;;
 379     sparc*)  ZERO_ARCHDEF=SPARC ;;
 380     x86_64*) ZERO_ARCHDEF=AMD64 ;;
 381     x86)     ZERO_ARCHDEF=IA32  ;;
 382     *)      ZERO_ARCHDEF=$(echo "${OPENJDK_TARGET_CPU_LEGACY_LIB}" | tr a-z A-Z)
 383   esac
 384   AC_SUBST(ZERO_ARCHDEF)
 385 ])
 386 
 387 AC_DEFUN([PLATFORM_SET_RELEASE_FILE_OS_VALUES],
 388 [
 389   if test "x$OPENJDK_TARGET_OS" = "xsolaris"; then
 390     REQUIRED_OS_NAME=SunOS
 391     REQUIRED_OS_VERSION=5.10
 392   fi
 393   if test "x$OPENJDK_TARGET_OS" = "xlinux"; then
 394     REQUIRED_OS_NAME=Linux
 395     REQUIRED_OS_VERSION=2.6
 396   fi
 397   if test "x$OPENJDK_TARGET_OS" = "xwindows"; then
 398     REQUIRED_OS_NAME=Windows
 399     if test "x$OPENJDK_TARGET_CPU_BITS" = "x64"; then
 400       REQUIRED_OS_VERSION=5.2
 401     else
 402       REQUIRED_OS_VERSION=5.1
 403     fi
 404   fi
 405   if test "x$OPENJDK_TARGET_OS" = "xmacosx"; then
 406     REQUIRED_OS_NAME=Darwin
 407     REQUIRED_OS_VERSION=11.2
 408   fi
 409 
 410   AC_SUBST(REQUIRED_OS_NAME)
 411   AC_SUBST(REQUIRED_OS_VERSION)
 412 ])
 413 
 414 #%%% Build and target systems %%%
 415 AC_DEFUN_ONCE([PLATFORM_SETUP_OPENJDK_BUILD_AND_TARGET],
 416 [
 417   # Figure out the build and target systems. # Note that in autoconf terminology, "build" is obvious, but "target"
 418   # is confusing; it assumes you are cross-compiling a cross-compiler (!)  and "target" is thus the target of the
 419   # product you're building. The target of this build is called "host". Since this is confusing to most people, we
 420   # have not adopted that system, but use "target" as the platform we are building for. In some places though we need
 421   # to use the configure naming style.
 422   AC_CANONICAL_BUILD
 423   AC_CANONICAL_HOST
 424   AC_CANONICAL_TARGET
 425 
 426   PLATFORM_EXTRACT_TARGET_AND_BUILD
 427   PLATFORM_SETUP_TARGET_CPU_BITS
 428   PLATFORM_SET_RELEASE_FILE_OS_VALUES
 429   PLATFORM_SETUP_LEGACY_VARS
 430 ])
 431 
 432 AC_DEFUN_ONCE([PLATFORM_SETUP_OPENJDK_BUILD_OS_VERSION],
 433 [
 434   ###############################################################################
 435 
 436   # Note that this is the build platform OS version!
 437 
 438   OS_VERSION="`uname -r | ${SED} 's!\.! !g' | ${SED} 's!-! !g'`"
 439   OS_VERSION_MAJOR="`${ECHO} ${OS_VERSION} | ${CUT} -f 1 -d ' '`"
 440   OS_VERSION_MINOR="`${ECHO} ${OS_VERSION} | ${CUT} -f 2 -d ' '`"
 441   OS_VERSION_MICRO="`${ECHO} ${OS_VERSION} | ${CUT} -f 3 -d ' '`"
 442   AC_SUBST(OS_VERSION_MAJOR)
 443   AC_SUBST(OS_VERSION_MINOR)
 444   AC_SUBST(OS_VERSION_MICRO)
 445 ])
 446 
 447 # Support macro for PLATFORM_SETUP_OPENJDK_TARGET_BITS.
 448 # Add -mX to various FLAGS variables.
 449 AC_DEFUN([PLATFORM_SET_COMPILER_TARGET_BITS_FLAGS],
 450 [
 451   # When we add flags to the "official" CFLAGS etc, we need to
 452   # keep track of these additions in ADDED_CFLAGS etc. These
 453   # will later be checked to make sure only controlled additions
 454   # have been made to CFLAGS etc.
 455   ADDED_CFLAGS=" ${COMPILER_TARGET_BITS_FLAG}${OPENJDK_TARGET_CPU_BITS}"
 456   ADDED_CXXFLAGS=" ${COMPILER_TARGET_BITS_FLAG}${OPENJDK_TARGET_CPU_BITS}"
 457   ADDED_LDFLAGS=" ${COMPILER_TARGET_BITS_FLAG}${OPENJDK_TARGET_CPU_BITS}"
 458 
 459   CFLAGS="${CFLAGS}${ADDED_CFLAGS}"
 460   CXXFLAGS="${CXXFLAGS}${ADDED_CXXFLAGS}"
 461   LDFLAGS="${LDFLAGS}${ADDED_LDFLAGS}"
 462 
 463   CFLAGS_JDK="${CFLAGS_JDK}${ADDED_CFLAGS}"
 464   CXXFLAGS_JDK="${CXXFLAGS_JDK}${ADDED_CXXFLAGS}"
 465   LDFLAGS_JDK="${LDFLAGS_JDK}${ADDED_LDFLAGS}"
 466 ])
 467 
 468 AC_DEFUN_ONCE([PLATFORM_SETUP_OPENJDK_TARGET_BITS],
 469 [
 470   ###############################################################################
 471   #
 472   # Now we check if libjvm.so will use 32 or 64 bit pointers for the C/C++ code.
 473   # (The JVM can use 32 or 64 bit Java pointers but that decision
 474   # is made at runtime.)
 475   #
 476 
 477   if test "x$OPENJDK_TARGET_OS" = xsolaris || test "x$OPENJDK_TARGET_OS" = xaix; then
 478     # Always specify -m flag on Solaris
 479     # And -q on AIX because otherwise the compiler produces 32-bit objects by default
 480     PLATFORM_SET_COMPILER_TARGET_BITS_FLAGS
 481   elif test "x$COMPILE_TYPE" = xreduced; then
 482     if test "x$OPENJDK_TARGET_OS_TYPE" = xunix; then
 483       # Specify -m if running reduced on unix platforms
 484       PLATFORM_SET_COMPILER_TARGET_BITS_FLAGS
 485     fi
 486   fi
 487 
 488   # Make compilation sanity check
 489   AC_CHECK_HEADERS([stdio.h], , [
 490     AC_MSG_NOTICE([Failed to compile stdio.h. This likely implies missing compile dependencies.])
 491     if test "x$COMPILE_TYPE" = xreduced; then
 492       AC_MSG_NOTICE([You are doing a reduced build. Check that you have 32-bit libraries installed.])
 493     elif test "x$COMPILE_TYPE" = xcross; then
 494       AC_MSG_NOTICE([You are doing a cross-compilation. Check that you have all target platform libraries installed.])
 495     fi
 496     AC_MSG_ERROR([Cannot continue.])
 497   ])
 498 
 499   AC_CHECK_SIZEOF([int *], [1111])
 500 
 501   # AC_CHECK_SIZEOF defines 'ac_cv_sizeof_int_p' to hold the number of bytes used by an 'int*'
 502   if test "x$ac_cv_sizeof_int_p" = x; then
 503     # The test failed, lets stick to the assumed value.
 504     AC_MSG_WARN([The number of bits in the target could not be determined, using $OPENJDK_TARGET_CPU_BITS.])
 505   else
 506     TESTED_TARGET_CPU_BITS=`expr 8 \* $ac_cv_sizeof_int_p`
 507 
 508     if test "x$TESTED_TARGET_CPU_BITS" != "x$OPENJDK_TARGET_CPU_BITS"; then
 509       # This situation may happen on 64-bit platforms where the compiler by default only generates 32-bit objects
 510       # Let's try to implicitely set the compilers target architecture and retry the test
 511       AC_MSG_NOTICE([The tested number of bits in the target ($TESTED_TARGET_CPU_BITS) differs from the number of bits expected to be found in the target ($OPENJDK_TARGET_CPU_BITS).])
 512       AC_MSG_NOTICE([I'll retry after setting the platforms compiler target bits flag to ${COMPILER_TARGET_BITS_FLAG}${OPENJDK_TARGET_CPU_BITS}])
 513       PLATFORM_SET_COMPILER_TARGET_BITS_FLAGS
 514 
 515       # We have to unset 'ac_cv_sizeof_int_p' first, otherwise AC_CHECK_SIZEOF will use the previously cached value!
 516       unset ac_cv_sizeof_int_p
 517       # And we have to undef the definition of SIZEOF_INT_P in confdefs.h by the previous invocation of AC_CHECK_SIZEOF
 518       cat &gt;&gt;confdefs.h &lt;&lt;_ACEOF
 519 #undef SIZEOF_INT_P
 520 _ACEOF
 521 
 522       AC_CHECK_SIZEOF([int *], [1111])
 523 
 524       TESTED_TARGET_CPU_BITS=`expr 8 \* $ac_cv_sizeof_int_p`
 525 
 526       if test "x$TESTED_TARGET_CPU_BITS" != "x$OPENJDK_TARGET_CPU_BITS"; then
 527         AC_MSG_ERROR([The tested number of bits in the target ($TESTED_TARGET_CPU_BITS) differs from the number of bits expected to be found in the target ($OPENJDK_TARGET_CPU_BITS)])
 528       fi
 529     fi
 530   fi
 531 
 532   AC_MSG_CHECKING([for target address size])
 533   AC_MSG_RESULT([$OPENJDK_TARGET_CPU_BITS bits])
 534 ])
 535 
 536 AC_DEFUN_ONCE([PLATFORM_SETUP_OPENJDK_TARGET_ENDIANNESS],
 537 [
 538   ###############################################################################
 539   #
 540   # Is the target little of big endian?
 541   #
 542   AC_C_BIGENDIAN([ENDIAN="big"],[ENDIAN="little"],[ENDIAN="unknown"],[ENDIAN="universal_endianness"])
 543 
 544   if test "x$ENDIAN" = xuniversal_endianness; then
 545     AC_MSG_ERROR([Building with both big and little endianness is not supported])
 546   fi
 547   if test "x$ENDIAN" != "x$OPENJDK_TARGET_CPU_ENDIAN"; then
 548     AC_MSG_ERROR([The tested endian in the target ($ENDIAN) differs from the endian expected to be found in the target ($OPENJDK_TARGET_CPU_ENDIAN)])
 549   fi
 550 ])
</pre></body></html>
